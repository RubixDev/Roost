LetterOrUnderscore  = 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J'
                     | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T'
                     | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z' | 'a' | 'b' | 'c' | 'd'
                     | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n'
                     | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x'
                     | 'y' | 'z' | '_' ;
digit   = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' ;
char    = ? any UTF8 character ? ;
EOL     = ? LF ? | ';' ;
EOF     = ? EOF ? ;

bool        = 'true' | 'false' ;
string      = '"' , { char - '"' } , '"'
            | "'" , { char - "'" } , "'" ;
identifier  = LetterOrUnderscore , { LetterOrUnderscore | digit } ;
number      = digit , { digit } , [ '.' , digit , { digit } ] ;

Expression                  = TernaryExpression , [ ( '..' | '..=' ) , TernaryExpression ] ;
TernaryExpression           = OrExpression , [ '?' , Expression , ':' , Expression ] ;
OrExpression                = AndExpression , { '|' , AndExpression } ;
AndExpression               = EqualityExpression , { '&' , EqualityExpression } ;
EqualityExpression          = RelationalExpression , { ( '==' | '!=' ) , RelationalExpression } ;
RelationalExpression        = AdditiveExpression , { ( '<' | '>' | '<=' | '>=' ) , AdditiveExpression } ;
AdditiveExpression          = MultiplicativeExpression , { ( '+' | '-' ) , MultiplicativeExpression } ;
MultiplicativeExpression    = UnaryExpression , { ( '*' | '/' ) , UnaryExpression } ;
UnaryExpression             = ( '+' | '-' | '!' ) , UnaryExpression
                            | '(' , Expression , ')'
                            | Atom ;
Atom                        = number | bool | string | identifier | CallExpression | 'null' ;
Arguments                   = '(' , [ Expression , { ',' , Expression } ] , ')' ;
CallExpression              = identifier , Arguments , { '.' , identifier , Arguments } ;

Statement           = DeclareStatement
                    | AssignStatement
                    | IfStatement
                    | LoopStatement
                    | WhileStatement
                    | ForStatement
                    | FunctionDeclaration
                    | Expression
                    | BreakStatement
                    | ContinueStatement
                    | ReturnStatement ;
DeclareStatement    = 'var' , identifier , '=' , Expression ;
AssignStatement     = identifier , ( '=' | '+=' | '-=' | '*=' | '/=' ) , Expression ;
IfStatement         = 'if' , '(' , Expression , ')' , Block , [ 'else' , Block ] ;
LoopStatement       = 'loop' , Block ;
WhileStatement      = 'while' , '(' , Expression , ')' , Block ;
ForStatement        = 'for' , '(' , identifier , 'in' , Expression , ')' , Block ;
FunctionDeclaration = 'fun' , identifier , '(' , [ identifier , { ',' , identifier } ] , ')' , Block ;
BreakStatement      = 'break' ;
ContinueStatement   = 'continue' ;
ReturnStatement     = 'return' , [ Expression ] ;

Statements  = { EOL } , [ Statement , { EOL , { EOL } , Statement } ] , { EOL } ;
Block       = '{' , Statements , '}' | Statement ;
Program     = Statements , EOF ;
